         TITLE '*** DASDSUB ***'
DASDSUB  MOWSTART
DASDSUB  AMODE 31
DASDSUB  RMODE ANY
**********************************************************************
* DASD SUB IS A ROUTINE TO GET CERTAIN DETAILS ABOUT A DASD DEVICE   *
* AND RETURN THEM TO THE CALLER. IT IS USED BY SVTOC FOR ONE.        *
* CALL DASDSUB(VOLSER,CYLS,TRKS/CYL,DSCBS/TRK,TYPE).                 *
**********************************************************************
         LR    R10,R1           COPY PARM POINTER
         L     R3,0(R10)        POINT TO CALLERS VOLSER
         XC    UWORK,UWORK      CLEAR WORK AREA
         UCBSCAN COPY,WORKAREA=UWORK,UCBAREA=UAREA,DCEAREA=UDCE,       *
               DCELEN=UDCEL,VOLSER=(R3),DYNAMIC=YES,RANGE=ALL
         SPACE 1
         LA    R9,UDCE                  A(DEVICE CLASS EXTENSION)
         USING DCE,R9
         L     R2,4(R10)                A(2ND ARG), CYLS PER VOL
         LH    R3,DCEUDCYL              # CYLS
         BCTR  R3,0                     LESS ONE
         ST    R3,0(R2)                 SAVE IT IN CALLERS AREA
         L     R2,8(R10)                A(3RD ARG), TRKS PER CYL
         LA    R3,15                    WE'LL CHEAT FOR NOW
         ST    R3,0(R2)                 SAVE IT IN CALLERS AREA
         L     R8,DCEVIB                GET ADDR OF VIB
         USING VIB,R8
         L     R2,12(R10)               A(4TH ARG), DSCBS PER TRK
         SR    R3,R3
         IC    R3,VIBDSCB               GET IT FROM VIB
         ST    R3,0(R2)                 SAVE IN CALLERS AREA
         L     R2,16(R10)               A(5TH ARG), DEV TYPE
         MVC   0(5,R2),=C'3380 '        A 3380
         MVI   4(R2),C'D'
         CLI   DCEMDRDT,D3380D          A 'D' ?
         BE    UCBEND
         MVI   4(R2),C'J'
         CLI   DCEMDRDT,D3380J          A 'J' ?
         BE    UCBEND
         MVI   4(R2),C'E'
         CLI   DCEMDRDT,D3380E          AN 'E' ?
         BE    UCBEND
         MVI   4(R2),C'K'
         CLI   DCEMDRDT,D3380K          A 'K'?
         BE    UCBEND
         MVI   4(R2),C' '               BLANK
         CLI   DCEMDRDT,D3380           A STANDARD? OR CAN'T TELL
         BE    UCBEND
         MVC   0(5,R2),=C'3390 '        A 3380
         MVI   4(R2),C'1'
         CLI   DCEMDRDT,D33901          A '3390-01'?
         BE    UCBEND
         MVI   4(R2),C'2'
         CLI   DCEMDRDT,D33902          A '3390-02'?
         BE    UCBEND
         MVI   4(R2),C'3'
         CLI   DCEMDRDT,D33903          A '3390-03'?
         MVI   4(R2),C'9'
         CLI   DCEMDRDT,D33909          A '3390-09'?
         BE    UCBEND
UCBEND   EQU   *
         MOREND
         DASDDEVC
*  UCB LOOKUP WORK AREA AND PARM LIST VARIABLES
UWORK    DC    XL100'00'         WORK AREA, MUST BE ZEROED
UAREA    DC    XL48'00'          UCB COPY
UDCE     DC    XL128'00'         UCB DEVICE CLASS EXTENSION
UDCEL    DC    AL2(*-UDCE)       LENGTH OF DCE EXTENSION AREA
         SPACE 1
VIB      DSECT                   VTOC INFO BLOCK
VIBID    DC    CL4'VIB '         EYECATCHER
         DC    CL2' '
         DC    CL2' '
VIBDSCB  DC    X'00'             DSCBS PER TRACK
         DC    X'000000'
         DC    CL4' '
         DC    CL2' '
         DC    CL2' '
         DC    CL2' '
         DC    CL2' '
         DC    CL2' '
VIBSER   DC    CL6' '            VOLSER
         PRINT NOGEN
         IECDDCE
         END
